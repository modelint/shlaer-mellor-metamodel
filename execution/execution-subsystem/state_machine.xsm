metadata
    Title : State Machine
    Author : Leon Starr
    Document ID : mint.meta.ego.td.19
    Creation date : Feburary 17, 2025
    Modification date : March 25, 2025
    Version : 0.1.1
    Organization : Model Integration, LLC / www.modelint.com
    Copyright notice > MIT
    Organization logo > mint
domain Metamodel
class State Machine
events
    New input
    No event from self
    Process active event
    Event ignored
    No events
    Transition response
    Enter state
    Ignore response
    No response
    Activity complete
--
state WAITING FOR EVENT
activity
transitions
    New input > Event from self?
--
state Event from self?
activity
    // Get the earliest self directed event, if any
    self_ev .= R2009/R2008/Self Directed Event(^-Order)
    self_ev? {
        // Event to self pending: make it the active event and process
        &R2012 self_ev/R2008/Dispatched Event <event selected>
        <event selected> Process active event -> me
        } : No event from self -> me // otherwise...
transitions
    No event from self > Non self event?
    Process active event > Determine response
--
state Non self event?
activity
    // Get any pending non self directed event
    non_self_ev .= R2009/R2008/Non Self Directed Event(1)
    non_self_ev? {
        // Make it the active event and process
        &R2012 non_self_ev/R2008/Dispatched Event <event selected>
        <event selected> Process active event -> me
        } : No events -> me // otherwise...
transitions
    No events > WAITING FOR EVENT
    Process active event > Determine response
--
state Determine response
activity
    // respond to this event
    event .= /R2012/Active Event
    // Get its name and response
    ev_name = event/R2012/Dispatched Event/R2010/Effective Event.Name
    response .= /R2005/Real State/R505/Event Reponse(Event: ev_name)
    // Transition? Save new state on R2014 and process it
    tr .= repsonse/R506/Transition
    tr? {
        &R2014 tr/R507/Real State <dest state saved>
        <dest state saved> Transition response -> me
    } : {
        // Must be ignore or can't happen
        non_tr .= response/R506/Non Transition
        non_tr.Behavior == .IGN? // Ignore?
            Ignore response -> : No response -> me
    }
transitions
    Transition response > STATE ENTRY
    Ignore response > Event processed
    No response > CANT HAPPEN
--
state STATE ENTRY
activity
    // Create/initiate activity execution and wait for completion
    Execute *> Activity Execution &R2015 me, /R504/State Activity
transitions
    Activity complete > Event processed
--
state Transition
activity
    // Update current state
    {
        &R2005 /R2014/Real State
        !& /R2014/Real State
    } <new current state>
    <new current state> Enter state -> me
transitions
    Enter state > STATE ENTRY
--
state Event processed
activity
    // Delete dispatched event
    {
        disp_ev .= /R2012/Dispatched Event
        sd_ev .= disp_ev/Self Directed Event
        nsd_ev .= disp_ev/Non Self Directed Event
        active_ev .= /R2012/Active Event
        pvals ..= disp_ev/R2013/Supplied Parameter Values
        !* disp_ev, sd_ev, nsd_ev, active_ev, pvals
    } <deleted>
    <deleted> Event ignored > me
transitions
    Event ignored > Event from self?
--
state CANT HAPPEN
activity
--
